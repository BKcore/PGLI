
<document> ::= "{" <s> (({ <docelement> <listsep> } <docelement>) | [<docelement>]) <s> "}"
	
	<docelement> ::= <attribute> | <params> | <vars> | <layers> | <fill> 
			
		<attribute> ::= <name> | <desc> | <width> | <height> | <origin>

			<name> ::= '"name"' <attrsep> <string>
			<desc> ::= '"desc"' <attrsep> <string>
			<width> ::= '"width"' <attrsep> <int>
			<height> ::= '"height"' <attrsep> <int>
			<origin> ::= '"origin"' <attrsep> ("bottom left" | "top left" | "center" | "bottom right" | "top right" | "inherit")


		<params> ::= '"params"' <attrsep> "{" <s> (({ <param> <listsep> } <param>) | [<param>]) <s> "}"
			
			<param>::= <string> <attrsep> (<string> | <number>)


		<vars> ::= '"vars"' <attrsep> "{" <s> (({ <var> <listsep> } <var> ) | [<var>]) <s> "}"
			
			<var> ::= <string> <attrsep> (<expression> | <number>)


		<layers> ::= '"layers"' <attrsep> "[" <s> (({ <layer> <listsep> } <layer> ) | [<layer>]) <s> "]"

			<layer> ::= "{" <s> (({ <layerparam> <listsep> } <layerparam> ) | [<layerparam>]) <s> "}"
				
				<layerparam> ::= <layeruse> | <layerrepeat> | <var>

					<layeruse> ::= '"use"' <attrsep> <pathexpr>

					<layerrepeat> ::= '"repeat"' <attrsep> <repeatexpr>

						<repeatexpr> ::= '"' <so> <exprvar> <so> <expritem> <so> <operatorrep> <so> <expritem> <so> '"'


		<fill> ::= '"fill"' <attrsep> "{" <s> <filltype> <listsep> <fillvalue> [<listsep> <fillrepeat>] <s> "}"

			<filltype> ::= '"type"' <attrsep> ("color" | "image")
			<fillvalue> ::= '"value"' <attrsep> <string>
			<fillrepeat> ::= '"repeat"' <attrsep> ('exact' | 'fit' | 'cover' | 'x' | 'y' | 'xy' )


<expression> ::= '"' (({ <expritem> <so> <operator> <so> } <expritem> ) | [<expritem>]) '"'
	
	<expritem> ::= <exprvar> | <exprfunc> | <number>

		<exprvar> ::= '@' <word>
		<exprfunc> ::= '#' <word> '(' (({ <exprfuncparam> <listsep> } <exprfuncparam>) | [<exprfuncparam>]) ')'

			<exprfuncparam> ::= <exprvar> | <number>

<pathexpr> ::= '"' (({ <pathexpritem> <pathsep> } <pathexpritem> ) | [<pathexpritem>]) '.pmod"'

	<pathexpritem> ::= <word> | <exprvar> | <exprfunc> | '.' | '~' | <pathexpritem>
	<pathsep> ::= '/' | '\\'


<string> ::= '"' <word> '"'
<number> ::= <int> | <float>
<operator> ::= '+' | '-' | '*' | '/' | '%'
<operatorrep> ::= '>' | '<' | '!=' | '<=' | '>='


<s> ::= { ' ' | '\n' | '\r\n' | '\t' }
<so> ::= { ' ' }
<listsep> ::= <s> ',' <s>
<attrsep> ::= <s> ':' <s>
